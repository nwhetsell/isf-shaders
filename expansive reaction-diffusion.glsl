/*{
    "CATEGORIES": [
        "Filter",
        "Generator"
    ],
    "CREDIT": "Flexi <https://www.shadertoy.com/user/Flexi>",
    "DESCRIPTION": "Reaction-diffusion system, converted from <https://www.shadertoy.com/view/4dcGW2>",
    "INPUTS": [
        {
            "NAME": "inputImage",
            "TYPE": "image"
        }
    ],
    "ISFVSN": "2",
    "PASSES": [
        {
            "TARGET": "bufferA",
            "PERSISTENT": true,
            "FLOAT": true
        },
        {
            "TARGET": "bufferB",
            "PERSISTENT": true,
            "FLOAT": true
        },
        {
            "TARGET": "bufferC",
            "PERSISTENT": true,
            "FLOAT": true
        },
        {

        }
    ]
}*/

#define iResolution RENDERSIZE

#include "lygia/generative/random.glsl"
#include "lygia/math/const.glsl"
#define INV_HALF_PI (0.5 * INV_PI)

// main reaction-diffusion loop

// actually the diffusion is realized as a separated two-pass Gaussian blur kernel and is stored in buffer C

vec2 complex_mul(vec2 factorA, vec2 factorB){
    return vec2( factorA.x*factorB.x - factorA.y*factorB.y, factorA.x*factorB.y + factorA.y*factorB.x);
}

vec2 complex_div(vec2 numerator, vec2 denominator){
    return vec2( numerator.x*denominator.x + numerator.y*denominator.y,
                numerator.y*denominator.x - numerator.x*denominator.y)/
        vec2(denominator.x*denominator.x + denominator.y*denominator.y);
}

float circle(vec2 uv, vec2 aspect, float scale){
    return clamp( 1. - length((uv-0.5)*aspect*scale), 0., 1.);
}

float sigmoid(float x) {
    return 2./(1. + exp2(-x)) - 1.;
}

float conetip(vec2 uv, vec2 pos, float size, float min)
{
    vec2 aspect = vec2(1.,iResolution.y/iResolution.x);
    return max( min, 1. - length((uv - pos) * aspect / size) );
}

float warpFilter(vec2 uv, vec2 pos, float size, float ramp)
{
    return 0.5 + sigmoid( conetip(uv, pos, size, -16.) * ramp) * 0.5;
}

vec2 vortex_warp(vec2 uv, vec2 pos, float size, float ramp, vec2 rot)
{
    vec2 aspect = vec2(1.,iResolution.y/iResolution.x);

    vec2 pos_correct = 0.5 + (pos - 0.5);
    vec2 rot_uv = pos_correct + complex_mul((uv - pos_correct)*aspect, rot)/aspect;
    float _filter = warpFilter(uv, pos_correct, size, ramp);
    return mix(uv, rot_uv, _filter);
}

vec2 vortex_pair_warp(vec2 uv, vec2 pos, vec2 vel)
{
    vec2 aspect = vec2(1.,iResolution.y/iResolution.x);
    float ramp = 5.;

    float d = 0.2;

    float l = length(vel);
    vec2 p1 = pos;
    vec2 p2 = pos;

    if(l > 0.){
        vec2 normal = normalize(vel.yx * vec2(-1., 1.))/aspect;
        p1 = pos - normal * d / 2.;
        p2 = pos + normal * d / 2.;
    }

    float w = l / d * 2.;

    // two overlapping rotations that would annihilate when they were not displaced.
    vec2 circle1 = vortex_warp(uv, p1, d, ramp, vec2(cos(w),sin(w)));
    vec2 circle2 = vortex_warp(uv, p2, d, ramp, vec2(cos(-w),sin(-w)));
    return (circle1 + circle2) / 2.;
}

// vec2 mouseDelta(){
//     vec2 pixelSize = 1. / iResolution.xy;
//     float eighth = 1./8.;
//     vec4 oldMouse = IMG_NORM_PIXEL(iChannel2, vec2(7.5 * eighth, 2.5 * eighth));
//     vec4 nowMouse = vec4(iMouse.xy / iResolution.xy, iMouse.zw / iResolution.xy);
//     if(oldMouse.z > pixelSize.x && oldMouse.w > pixelSize.y &&
//        nowMouse.z > pixelSize.x && nowMouse.w > pixelSize.y)
//     {
//         return nowMouse.xy - oldMouse.xy;
//     }
//     return vec2(0.);
// }

#define fragCoord gl_FragCoord.xy
#define fragColor gl_FragColor
#define iFrame FRAMEINDEX
#define iMouse vec2(0)
#define iTime TIME

void main()
{
    if (PASSINDEX == 0) // Shadertoy Buffer A
    {
        vec2 uv = fragCoord.xy / iResolution.xy;
        vec2 pixelSize = 1. / iResolution.xy;


        // vec2 mouseV = mouseDelta();
        vec2 aspect = vec2(1.,iResolution.y/iResolution.x);
        // uv = vortex_pair_warp(uv, iMouse.xy*pixelSize, mouseV*aspect*1.4);

        vec4 blur1 = IMG_NORM_PIXEL(bufferC, uv);

        vec4 noise = random4(uv + fract(vec2(42,56)*iTime));// IMG_NORM_PIXEL(iChannel3, fragCoord.xy / iChannelResolution[3].xy + fract(vec2(42,56)*iTime));

        // get the gradients from the blurred image
        vec2 d = pixelSize*4.;
        vec4 dx = (IMG_NORM_PIXEL(bufferC, fract(uv + vec2(1,0)*d)) - IMG_NORM_PIXEL(bufferC, fract(uv - vec2(1,0)*d))) * 0.5;
        vec4 dy = (IMG_NORM_PIXEL(bufferC, fract(uv + vec2(0,1)*d)) - IMG_NORM_PIXEL(bufferC, fract(uv - vec2(0,1)*d))) * 0.5;

        vec2 uv_red = uv + vec2(dx.x, dy.x)*pixelSize*8.; // add some diffusive expansion

        float new_red = IMG_NORM_PIXEL(bufferA, fract(uv_red)).x + (noise.x - 0.5) * 0.0025 - 0.002; // stochastic decay
        new_red -= (IMG_NORM_PIXEL(bufferC, fract(uv_red + (noise.xy-0.5)*pixelSize)).x -
                    IMG_NORM_PIXEL(bufferA, fract(uv_red + (noise.xy-0.5)*pixelSize))).x * 0.047; // reaction-diffusion

        if (iFrame < 10) {
            fragColor = noise;
        } else {
            fragColor.x = clamp(new_red, 0., 1.);
        }
    }
    else if (PASSINDEX == 1) // Shadertoy Buffer B
    {
        vec2 pixelSize = 1. / RENDERSIZE;
        vec2 uv = fragCoord.xy * pixelSize;

        float h = pixelSize.x;
        vec4 sum = vec4(0.0);
        sum += IMG_NORM_PIXEL(bufferA, fract(vec2(uv.x - 4.0*h, uv.y)) ) * 0.05;
        sum += IMG_NORM_PIXEL(bufferA, fract(vec2(uv.x - 3.0*h, uv.y)) ) * 0.09;
        sum += IMG_NORM_PIXEL(bufferA, fract(vec2(uv.x - 2.0*h, uv.y)) ) * 0.12;
        sum += IMG_NORM_PIXEL(bufferA, fract(vec2(uv.x - 1.0*h, uv.y)) ) * 0.15;
        sum += IMG_NORM_PIXEL(bufferA, fract(vec2(uv.x + 0.0*h, uv.y)) ) * 0.16;
        sum += IMG_NORM_PIXEL(bufferA, fract(vec2(uv.x + 1.0*h, uv.y)) ) * 0.15;
        sum += IMG_NORM_PIXEL(bufferA, fract(vec2(uv.x + 2.0*h, uv.y)) ) * 0.12;
        sum += IMG_NORM_PIXEL(bufferA, fract(vec2(uv.x + 3.0*h, uv.y)) ) * 0.09;
        sum += IMG_NORM_PIXEL(bufferA, fract(vec2(uv.x + 4.0*h, uv.y)) ) * 0.05;

        fragColor.xyz = sum.xyz/0.98; // normalize
        fragColor.a = 1.;
    }
    else if (PASSINDEX == 2) // Shadertoy Buffer C
    {
        vec2 pixelSize = 1. / RENDERSIZE;
        vec2 uv = fragCoord.xy * pixelSize;

        float v = pixelSize.y;
        vec4 sum = vec4(0.0);
        sum += IMG_NORM_PIXEL(bufferB, fract(vec2(uv.x, uv.y - 4.0*v)) ) * 0.05;
        sum += IMG_NORM_PIXEL(bufferB, fract(vec2(uv.x, uv.y - 3.0*v)) ) * 0.09;
        sum += IMG_NORM_PIXEL(bufferB, fract(vec2(uv.x, uv.y - 2.0*v)) ) * 0.12;
        sum += IMG_NORM_PIXEL(bufferB, fract(vec2(uv.x, uv.y - 1.0*v)) ) * 0.15;
        sum += IMG_NORM_PIXEL(bufferB, fract(vec2(uv.x, uv.y + 0.0*v)) ) * 0.16;
        sum += IMG_NORM_PIXEL(bufferB, fract(vec2(uv.x, uv.y + 1.0*v)) ) * 0.15;
        sum += IMG_NORM_PIXEL(bufferB, fract(vec2(uv.x, uv.y + 2.0*v)) ) * 0.12;
        sum += IMG_NORM_PIXEL(bufferB, fract(vec2(uv.x, uv.y + 3.0*v)) ) * 0.09;
        sum += IMG_NORM_PIXEL(bufferB, fract(vec2(uv.x, uv.y + 4.0*v)) ) * 0.05;

        fragColor.xyz = sum.xyz/0.98; // normalize
        fragColor.a = 1.;
    }
    else // Shadertoy Image
    {
        vec2 uv = fragCoord.xy / iResolution.xy;
        vec2 pixelSize = 1. / iResolution.xy;
        vec2 aspect = vec2(1.,iResolution.y/iResolution.x);

        vec4 noise = random4(uv + fract(vec2(42,56)*iTime));// IMG_NORM_PIXEL(iChannel3, fragCoord.xy / iChannelResolution[3].xy + fract(vec2(42,56)*iTime));

        vec2 lightSize=vec2(4.);

        // add the pixel gradients
        vec2 d = pixelSize*1.;
        vec4 dx = IMG_NORM_PIXEL(bufferA, uv + vec2(1,0)*d) - IMG_NORM_PIXEL(bufferA, uv - vec2(1,0)*d);
        vec4 dy = IMG_NORM_PIXEL(bufferA, uv + vec2(0,1)*d) - IMG_NORM_PIXEL(bufferA, uv - vec2(0,1)*d);

        vec2 displacement = vec2(dx.x,dy.x)*lightSize; // using only the red gradient as displacement vector
        float light = pow(
            max(1. - distance(0.5 + (uv - 0.5) * aspect * lightSize + displacement, 0.5 + (iMouse.xy * pixelSize - 0.5) * aspect * lightSize), 0.),
            4.
        );

        // recolor the red channel
        vec4 rd = vec4(IMG_NORM_PIXEL(bufferA,uv+vec2(dx.x,dy.x)*pixelSize*8.).x)*vec4(0.7,1.5,2.0,1.0)-vec4(0.3,1.0,1.0,1.0);

        // and add the light map
        fragColor = mix(
            rd,
            vec4(8, 6, 2, 1),
            light * 0.75 * vec4(1. - IMG_NORM_PIXEL(bufferA, uv + vec2(dx.x, dy.x) * pixelSize * 8.).x)
        );

        fragColor = rd;

        fragColor.a = 1.;
    }
}
